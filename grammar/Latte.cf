-- programs ------------------------------------------------

entrypoints Program ;

Program.   Program ::= [TopDef] ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Block ;

ClsDef.    TopDef ::= "class" Ident ClsExt ClsBody ;

separator nonempty TopDef "" ;

Arg. 	   Arg ::= Type Ident;

separator  Arg "," ;

-- classes -------------------------------------------------

Ext.       ClsExt ::= "extends" Ident ;

NoExt.     ClsExt ::= ;

ClsBody.   ClsBody ::= "{" [ClsStmt] "}" ;

MetDef.    ClsStmt ::= Type Ident "(" [Arg] ")" Block ;

AttrDef.   ClsStmt ::= Type [Item] ";" ;

separator ClsStmt "" ;

-- statements ----------------------------------------------

Block.     Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ;

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Ident "=" Expr  ";" ;

AttrAss.   Stmt ::= Ident [Mem] "=" Expr ";" ;

Incr.      Stmt ::= Ident "++"  ";" ;

AttrIncr.  Stmt ::= Ident [Mem] "++" ";" ;

Decr.      Stmt ::= Ident "--"  ";" ;

AttrDecr.  Stmt ::= Ident [Mem] "--" ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

ForEach.   Stmt ::= "for" "(" Type Ident ":" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

Array.     Type ::= Type "[]" ;

Class.     Type ::= Ident ;

internal   Fun. Type ::= Type "(" [Type] ")" ;

separator  Type "," ;

-- Expressions ---------------------------------------------

ELitInt.   Expr8 ::= Integer ;

ELitTrue.  Expr8 ::= "true" ;

ELitFalse. Expr8 ::= "false" ;

EApp.      Expr8 ::= Ident "(" [Expr] ")" ;

EString.   Expr8 ::= String ;

ELitNull.  Expr8 ::= "(" Expr ")" "null" ;

ELitArr.   Expr7 ::= "new" Type "[" Expr "]" ;

ELitCls.   Expr7 ::= "new" Type ;

EExprAttr. Expr7 ::= Expr8 [Mem] ;

ENameAttr. Expr7 ::= Ident [Mem] ;

MemVar.    Mem ::= "." Ident ;

MemInd.    Mem ::= "[" Expr "]" ;

MemMet.    Mem ::= "." Ident "(" [Expr] ")" ;

separator nonempty Mem "" ;

EVar.      Expr6 ::= Ident ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 8 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

